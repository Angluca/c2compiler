/* Copyright 2022-2025 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;
import src_loc local;

type StringLiteralBits struct {
    u32 : NumExprBits;
    u32 src_len : 32 - NumExprBits;
}

public type StringLiteral struct @(opaque) {
    Expr base;
    u32 value;
    u32 size; // Note: no cost on 64-bit systems because of padding
}

public fn StringLiteral* StringLiteral.create(ast_context.Context* c, SrcLoc loc, u32 src_len, u32 value, u32 len) {
    StringLiteral* e = c.alloc(sizeof(StringLiteral));
    e.base.init(ExprKind.StringLiteral, loc, 0, 1, 0, ValType.LValue);
    e.base.base.stringLiteralBits.src_len = src_len;
    e.value = value;
    e.size = len + 1;   // size includes the null terminator
#if AstStatistics
    Stats.addExpr(ExprKind.StringLiteral, sizeof(StringLiteral));
#endif
    e.base.setType(getStringType(len + 1));
    return e;
}

public fn const char* StringLiteral.getText(const StringLiteral* e) {
    return idx2name(e.value);
}

public fn u32 StringLiteral.getTextIndex(const StringLiteral* e) {
    return e.value;
}

// includes 0-terminator
public fn u32 StringLiteral.getSize(const StringLiteral* e) {
    return e.size;
}

public fn u32 StringLiteral.strlen(const StringLiteral* e) {
    return e.size - 1;
}

fn SrcLoc StringLiteral.getEndLoc(const StringLiteral* e) {
    return e.base.loc + e.base.base.stringLiteralBits.src_len;
}

public fn void StringLiteral.printLiteral(const StringLiteral* e, string_buffer.Buf* out) {
    out.add1('"');
    out.encodeBytes(idx2name(e.value), e.size - 1, '"');
    out.add1('"');
}

fn void StringLiteral.print(const StringLiteral* e, string_buffer.Buf* out, u32 indent) {
    e.base.printKind(out, indent);
    e.base.printTypeBits(out);
    out.space();
    out.color(col_Value);
    e.printLiteral(out);
    out.newline();
}

